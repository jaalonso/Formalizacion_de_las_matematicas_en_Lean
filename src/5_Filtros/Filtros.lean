-- Filtros

-- ---------------------------------------------------------------------
-- Ejercicio. Importar la teorÃ­a de filtros.
-- ---------------------------------------------------------------------

import order.filter.basic

-- =====================================================================
-- Â§ IntroducciÃ³n                                                     --
-- =====================================================================

-- Un espacio topolÃ³gico sobre un tipo `Î±` es una colecciÃ³n de
-- subconjuntos de `Î±` (los conjuntos abiertos) que satisfacen algunos
-- axiomas. Un filtro es un tipo similar: es una colecciÃ³n de
-- subconjuntos de `Î±` que satisfacen otros axiomas.
--
-- Antes de ver la definiciÃ³n formal, empecemos por la idea. Un filtro
-- en `Î±` es una generalizaciÃ³n muy potente del concepto de subconjunto
-- de `Î±`. Si `S : set Î±` es un subconjunto, entonces existe un filtro
-- `ğ“Ÿ S` correspondiente a `S` que es la colecciÃ³n de todos los
-- subconjuntos de `Î±` que contienen a `S`. Sin embargo, puede haber
-- otros filtros en `Î±` correspondientes a cosas que son un poco mÃ¡s
-- generales que los subconjuntos. Por ejemplo si `Î±` es un espacio
-- topolÃ³gico y `x : Î±` entonces hay un filtro `ğ“ x` correspondiente a
-- "un entorno infinitesimal de `x`" tal que no hay ningÃºn conjunto
-- abierto mÃ¡s pequeÃ±o que contenga a `x`. Como otro ejemplo, si `Î±`
-- estÃ¡ ordenado entonces hay un filtro de "entornos del infinito" en
-- `Î±` aunque aunque no haya ningÃºn `âˆ` en `Î±`.
--
-- Si `F` es un filtro, entonces se puede pensar en `F` como un tipo
-- generalizado de subconjunto `F` de `Î±`, y debes pensar que `S âˆˆ F`
-- significa `F âŠ† S`. Tener esto en cuenta te ayudarÃ¡ a motivar los
-- axiomas que vienen a continuaciÃ³n.

-- =====================================================================
-- Â§ DefiniciÃ³n de filtro                                             --
-- =====================================================================

-- Esta es la definiciÃ³n formal. Un filtro sobre `Î±` es una colecciÃ³n
-- `F` de subconjuntos de `Î±` que satisface los tres axiomas siguientes:
-- 1. `Î± âˆˆ F` (en Lean se escribe `univ âˆˆ F` porque se distingue
--    entre el tipo `Î±` y el tÃ©rmino `univ : set Î±` correspondiente a `Î±`)
-- 2. Si `S âˆˆ F` y `S âŠ† T` entonces `T âˆˆ F`; es decir, `F` es "cerrado
--    hacia arriba".
-- 3. Si `A âˆˆ F` y `B âˆˆ F` entonces `A âˆ© B âˆˆ F`; es decir, `F` es
--    cerrado bajo intersecciones binarias.
--
-- ObsÃ©rvese que (1) y (3) implican conjuntamente (y son de hecho equivalentes a
-- la afirmaciÃ³n de que `F` es cerrado bajo todas las intersecciones finitas;
-- es decir, la intersecciÃ³n de un nÃºmero finito de elementos de `F` estÃ¡ en `F`.
--
-- Esta es la definiciÃ³n de Lean:
--    structure filter (Î± : Type*) :=
--    (sets                   : set (set Î±))
--    (univ_sets              : set.univ âˆˆ sets)
--    (sets_of_superset {x y} : x âˆˆ sets â†’ x âŠ† y â†’ y âˆˆ sets)
--    (inter_sets {x y}       : x âˆˆ sets â†’ y âˆˆ sets â†’ x âˆ© y âˆˆ sets)
--
-- En palabras, `filter Î±` es el tipo de filtros sobre `Î±`, por lo que
-- dar un filtro en `Î±` es dar un tÃ©rmino `F : filter Î±` de tipo `filter
-- Î±`. Para obtener un tÃ©rmino de tipo `filter Î±` hay que dar una
-- colecciÃ³n `conjuntos` de subconjuntos de `Î±` y luego tres pruebas de
-- los tres axiomas.
--
-- Un ejemplo bastante simple de un filtro es el filtro de *todos* los
-- subconjuntos de `Î±`. Aquellos que hayan visto definiciones de
-- filtros en otros lugares (por ejemplo en Bourbaki) habrÃ¡n visto un
-- axioma adicional en la definiciÃ³n de un filtro que dice que un filtro
-- no puede ser la colecciÃ³n de todos los subconjuntos de subconjuntos
-- de `Î±`. Esto resulta ser un axioma poco natural, es como es como
-- exigir en la teorÃ­a de los ideales que si `R` es un anillo entonces
-- `R` no puede ser un ideal de `R`. Una ventaja de esta definiciÃ³n de
-- ideal serÃ­a que un ideal maximal de `R` serÃ­a literalmente un
-- elemento maximal de los ideales de `R`, pero esta ventaja se ve
-- compensada por la desventaja de que la definiciÃ³n se vuelve mucho
-- menos functorial (por ejemplo, la imagen de un ideal a lo largo de
-- un homomorfismo de anillo podrÃ­a no ser un ideal, no se puede en
-- general sumar dos ideales, etc.). Para preservar la funtorialidad de
-- los filtros, mathlib no tiene este axioma de Bourbaki como un axioma
-- para los filtros. En consecuencia, hay dos filtros "extremos" en `Î±`,
-- a saber, el que sÃ³lo contiene `univ` (nÃ³tese que esto es forzado por
-- `univ_sets`), y luego el que mencionamos antes, que contiene todos
-- los subconjuntos de `Î±`. Estos dos filtros se llaman `âŠ¥` y `âŠ¤`,
-- Â¡aunque te sorprenderÃ¡ saber cuÃ¡l es cada uno!  El filtro formado por
-- todos los subconjuntos de `Î±` es el que corresponde al conjunto vacÃ­o,
-- por lo que es `âŠ¥`, y el que consta sÃ³lo de `univ` es el es el
-- correspondiente al conjunto de `Î±`, por lo que es `âŠ¤`.

-- =====================================================================
-- Â§ NotaciÃ³n, tÃ¡cticas Ãºtiles y teoremas Ãºtiles                      --
-- =====================================================================

-- No vamos a construir filtros desde los primeros principios, en su
-- lugar usaremos la API de Lean para los filtros.
--
-- Sean `Î± : Type`, `F : filter Î±` y `S : set Î±`. La notaciÃ³n
-- `S âˆˆ F` se se define como `S âˆˆ F.sets`.
--
-- La tÃ¡ctica `ext` puede utilizarse para reducir un objetivo `F = G` a
-- un objetivo de la forma `âˆ€ S, S âˆˆ F â†” S âˆˆ G`.
--
-- Los campos de la estructura mencionan cosas como `S âˆˆ F.sets`, por lo
-- que los axiomas se repiten con otros nombres, pero utilizando la
-- notaciÃ³n `S âˆˆ F`. Los lemas correspondientes a las definiciones son:
--    `univ_mem_sets : univ âˆˆ F`
--    `mem_sets_of_superset : S âˆˆ F â†’ S âŠ† T â†’ T âˆˆ F`
--    `inter_mem_sets : S âˆˆ F â†’ T âˆˆ F â†’ S âˆ© T âˆˆ F`
--
-- Estos lemas estÃ¡n en el espacio de nombres `filter`; es decir, sus
-- nombres completos son `filter.univ_mem_sets`, etc. Pero usaremos
-- "open filter", lo que significa que no se necesita escribir el
-- prefijo `filter.` delante de cada lema que necesites sobre
-- filtros. De hecho, tambiÃ©n usaremos un montÃ³n de cosas sobre
-- conjuntos, como `set.inter_subset_left`, asÃ­ que tambiÃ©n usaremos
-- `open set`.

-- ---------------------------------------------------------------------
-- Ejercicio. Abrir los espacios de nombre de los filtros y de los
-- conjuntos.
-- ---------------------------------------------------------------------

open filter set

-- ---------------------------------------------------------------------
-- Ejercicio. Declara las variables
-- + Î± sobre tipos
-- + F sobre filtros sobre Î±
-- + S y T sobre conjunros de elementos de tipo Î±.
-- ---------------------------------------------------------------------

variable  (Î± : Type)
variable  (F : filter Î±)
variables (S T : set Î±)

-- He aquÃ­ un lema sobre los filtros: Dos conjuntos `S` y `T` estÃ¡n
-- ambos en un filtro `F` si su intersecciÃ³n lo estÃ¡. Para demostrarlo,
-- es Ãºtil conocer los siguientes resultados (del espacio de nombres de
-- los conjuntos)
-- + `inter_subset_left S T : S âˆ© T âŠ† S`
-- + `inter_subset_right S T : S âˆ© T âŠ† S`

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que
--    S âˆ© T âˆˆ F â†” S âˆˆ F âˆ§ T âˆˆ F
-- ---------------------------------------------------------------------

-- DemostraciÃ³n (por doble implicaciÃ³n):
-- (âŸ¹) Supongamos que S âˆ© T âˆˆ F. Entonces, S âˆ© T âŠ† S y, por el axioma
-- mem_sets_of_superset, se tiene que S âˆˆ F. AnÃ¡logamente se demuestra
-- que T âˆˆ F.
--
-- (âŸ¸) S âˆˆ F âˆ§ T âˆˆ F. Entonces, S âˆ© T âˆˆ F por el axioma inter_mem_sets.

example : S âˆ© T âˆˆ F â†” S âˆˆ F âˆ§ T âˆˆ F :=
begin
  split,
  { intro hST,
    split,
    { apply mem_sets_of_superset hST,
      exact inter_subset_left S T },
    { apply mem_sets_of_superset hST,
      exact inter_subset_right S T } },
  { rintros âŸ¨hS, hTâŸ©,
    exact inter_mem_sets hS hT }
end

-- El filtro principal `ğ“Ÿ X` generado por `X : set Î±` es el conjunto de
-- los subconjuntos de `Î±` que contienen a `X`. Se demostrarÃ¡ que es un
-- filtro. Para ello, son Ãºtiles los siguientes lemas:
--    `mem_univ s : s âˆˆ univ`
--    `subset.trans : A âŠ† B â†’ B âŠ† C â†’ A âŠ† C`
--    `subset_inter : X âŠ† S â†’ X âŠ† T â†’ X âŠ† S âˆ© T`
-- (nÃ³tese que probablemente podrÃ­as demostrar estas dos Ãºltimas cosas
-- directamente, pero tambiÃ©n podemos usar la interfaz para conjuntos
-- dado que existe)
--    `mem_set_of_eq : x âˆˆ {a : Î± | p a} = p x`
-- (este es por definiciÃ³n, asÃ­ que podrÃ­as usar `change` en su lugar, o
-- simplemente no reescribirlo en absoluto)

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que el filtro principal `ğ“Ÿ X` generado por `X`
-- es un filtro.
-- ---------------------------------------------------------------------

-- DemostraciÃ³n: Se define una estructura (que en mathlib se llama
-- `ğ“Ÿ X`) formada por los subconjuntos de Î± que contienen a X y se
-- prueba que cumple los axiomas de filtro. Concretamente, la definiciÃ³n
-- es
--    ğ“Ÿ X = {S : set Î± | X âŠ† S}
-- y las demostraciones de los axiomas son:
--
-- (1Âº axioma) univ âˆˆ ğ“Ÿ X ya que X âŠ† univ.
--
-- (2Âº axioma): Sean S âˆˆ ğ“Ÿ X y S âŠ† T. Entonces, X âŠ† S y, por la
-- transitiva, X âŠ† T. Por tanto, T âˆˆ ğ“Ÿ X.
--
-- (3Âº axioma): Sean S, T âˆˆ ğ“Ÿ X. Entonces, X âŠ† S y X âŠ† T. Por tanto,
-- X âŠ† S âˆ© T. Luego, S âˆ© T âˆˆ ğ“Ÿ X.

example (X : set Î±) : filter Î± :=
{ sets := {S : set Î± | X âŠ† S},
  univ_sets :=
  begin
    intros S hS,
    exact mem_univ _,
  end,
  sets_of_superset :=
  begin
    intros S T hS hT,
    change X âŠ† T,
    change X âŠ† S at hS,
    exact subset.trans hS hT,
  end,
  inter_sets :=
  begin
    intros S T hS hT,
    rw mem_set_of_eq at *,
    exact subset_inter hS hT,
  end }

-- ---------------------------------------------------------------------
-- Ejercicio. Abrir localmente el entorno filter para poder usar la
-- notaciÃ³n `ğ“Ÿ X` para el filtro principal generado por `X`.
-- ---------------------------------------------------------------------

open_locale filter

-- =====================================================================
-- Â§ El orden (â‰¤) sobre filtros                                       --
-- =====================================================================

-- Lo siguiente no es sorprendente: la colecciÃ³n de todos los filtros
-- sobre `Î±` estÃ¡ parcialmente ordenada. Tal vez sea mÃ¡s sorprendente:
-- el orden es al revÃ©s de lo que se esperarÃ­a. Si `F` y `G` son filtros
-- en `Î±`, entonces `F â‰¤ G` se define como `G.sets âŠ† F.sets`; es decir,
-- cada conjunto en el filtro `G` estÃ¡ tambiÃ©n en el filtro `F`. Â¿A quÃ©
-- se debe esto? Pues bien, piensa en los filtros principales. Si `S âŠ†
-- T` son dos subconjuntos, entonces `X âˆˆ ğ“Ÿ T` implica `T âŠ† X`, luego `S
-- âŠ† X` y `X âˆˆ ğ“Ÿ S`. Cuanto mÃ¡s pequeÃ±o sea el conjunto, mayor serÃ¡ la
-- colecciÃ³n de conjuntos en su filtro principal.
--
-- Formalicemos esto. Demostremos que ğ“Ÿ S â‰¤ ğ“Ÿ T â†” S âŠ† T.
-- NÃ³tese que esto es el lema `principal_mono` en mathlib pero
-- no hay inconveniente en demostrarlo por uno mismo.
--
-- Algunos lemas Ãºtiles para su demostraciÃ³n son:
-- + `le_def : F â‰¤ G â†” âˆ€ (S : set Î±), S âˆˆ G â†’ S âˆˆ F`
-- + `mem_principal_sets : T âˆˆ ğ“Ÿ S â†” S âŠ† T`
-- + `mem_principal_self S : S âˆˆ ğ“Ÿ S`

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que
--    ğ“Ÿ S â‰¤ ğ“Ÿ T â†” S âŠ† T
-- ---------------------------------------------------------------------

-- DemostraciÃ³n (por doble implicaciÃ³n)
-- (âŸ¹) Supongamos que ğ“Ÿ S â‰¤ ğ“Ÿ T.
--       T âŠ† T âŸ¹ T âˆˆ ğ“Ÿ T    [por definiciÃ³n de ğ“Ÿ T]
--             âŸ¹ T âˆˆ ğ“Ÿ S    [por definiciÃ³n de ğ“Ÿ S â‰¤ ğ“Ÿ T]
--             âŸ¹ S âŠ† T      [por definiciÃ³n de ğ“Ÿ S]
-- (âŸ¸) Supongamos que S âŠ† T. Para probar que ğ“Ÿ S â‰¤ ğ“Ÿ T, sea X âˆˆ ğ“Ÿ T.
-- Entonces,
--       X âˆˆ ğ“Ÿ T âŸ¹ T âŠ† X    [por definiciÃ³n de ğ“Ÿ T]
--               âŸ¹ S âŠ† X    [porque S âŠ† T]
--               âŸ¹ X âˆˆ ğ“Ÿ S  [por definiciÃ³n de ğ“Ÿ S]

-- 1Âª demostraciÃ³n
example : ğ“Ÿ S â‰¤ ğ“Ÿ T â†” S âŠ† T :=
begin
  split,
  { intro h,
    rw le_def at h,
    have hT : T âˆˆ ğ“Ÿ T := mem_principal_self T,
    specialize h T hT,
    rwa mem_principal_sets at h },
  { intro hST,
    rw le_def,
    intros X hX,
    rw mem_principal_sets at *,
    exact subset.trans hST hX }
end

-- 2Âª demostraciÃ³n
example : ğ“Ÿ S â‰¤ ğ“Ÿ T â†” S âŠ† T :=
-- by library_search
principal_mono

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que
--    F â‰¤ ğ“Ÿ S â†” S âˆˆ F
-- ---------------------------------------------------------------------

-- DemostraciÃ³n (por doble implicaciÃ³n)
-- (âŸ¹) Supongamos que F â‰¤ ğ“Ÿ S. Se tiene,
--    S âŠ† S âŸ¹ S âˆˆ ğ“Ÿ S   [por def. de ğ“Ÿ S]
--          âŸ¹ S âˆˆ F     [por def. de F â‰¤ ğ“Ÿ S]
-- (âŸ¸) Supongamos que S âˆˆ F. Para probar que F â‰¤ ğ“Ÿ S, sea X âˆˆ ğ“Ÿ S. Se
-- tiene,
--     X âˆˆ ğ“Ÿ S âŸ¹ S âŠ† X   [por def. de ğ“Ÿ S]
--             âŸ¹ X âˆˆ F   [porque F es un filtro y S âˆˆ F]

-- 1Âª demostraciÃ³n
example : F â‰¤ ğ“Ÿ S â†” S âˆˆ F :=
begin
  split,
  { intro h,
    rw le_def at h,
    apply h,
    exact mem_principal_self S,},
  { rw le_def,
    intros hSF X hX,
    rw mem_principal_sets at hX,
    exact mem_sets_of_superset hSF hX },
end

-- 2Âª demostraciÃ³n
example : F â‰¤ ğ“Ÿ S â†” S âˆˆ F :=
-- by library_search
le_principal_iff

-- =====================================================================
-- Â§ Los filtros son retÃ­culos completos                              --
-- =====================================================================

-- Al igual que es posible hablar del espacio topolÃ³gico generado por
-- una colecciÃ³n de subconjuntos de `Î±` (es la topologÃ­a mÃ¡s pequeÃ±a
-- para la que los subconjuntos dados son todos abiertos) tambiÃ©n es
-- posible hablar del filtro generado por una colecciÃ³n de subconjuntos
-- de `Î±`. Se puede definir como la intersecciÃ³n de todos los filtros
-- que contienen a la colecciÃ³n dada de subconjuntos.
--
-- En la teorÃ­a del orden, dado un orden parcial (como el orden parcial
-- de los filtros) se puede empezar a preguntar si existen Ã­nfimos y
-- supremos. Los filtros son un ejemplo donde todas estas cosas existen
-- (Ã­nfimos y supremos finitos e infinitos) y satisfacen una colecciÃ³n
-- natural de axiomas, convirtiÃ©ndolos en lo que se llama un *retÃ­culo
-- completo*. Se puede demostrar esto mostrando que "filtro generado por
-- estos conjuntos" y "conjuntos subyacentes de un filtro" son funtores
-- adjuntos y luego utilizando la teorÃ­a de las inserciones de
-- Galois. Ya hablÃ© un poco de esto al estudiar los subgrupos, y no
-- hablarÃ© de ello de nuevo.

-- =====================================================================
-- Â§ Otros ejemplos de filtros                                        --
-- =====================================================================

-- =====================================================================
-- Â§Â§ Filtro de los grandes en conjuntos totalmente ordenados         --
-- =====================================================================

-- Sea `L` un conjunto totalmente ordenado no vacÃ­o. Digamos que un
-- subconjunto `X` de `L` es "grande" si existe `x : L` tal que para
-- todo `y â‰¥ x`, `y âˆˆ X`. En mathlib, se denota pot `at_top L`.
--
-- Probaremos que los subconjuntos grandes son un filtro. La idea
-- matemÃ¡tica es que los "grandes subconjuntos" son las entornos de `âˆ`,
-- por lo que el correspondiente filtro es alguna representaciÃ³n de un
-- entorno infinitesimal de `âˆ`.
--
-- Notas de implementaciÃ³n: `linear_order L` es el tipo de Ã³rdenes
-- lineales sobre `L`.  AdemÃ¡s, `e : L` es una forma sencilla de decir
-- que `L` no es vacÃ­o.
--
-- Recordemos que `max x y` es el mÃ¡ximo de x e y en un `orden lineal`, y
-- `le_max_left a b : a â‰¤ max a b` y anÃ¡logamente `le_max_right`.

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que si L es un orden lineal no vacÃ­o, el
-- conjunto de sus conjuntos grandes es un filtro sobre L.
-- ---------------------------------------------------------------------

-- DemostraciÃ³n. El conjunto de los conjuntos grandes de L es
--    F = {X : set L | âˆƒ x : L, âˆ€ y, x â‰¤ y â†’ y âˆˆ X}
-- Tenemos que demostrar que F es un filtro probando los tres axiomas de
-- filtro.
--
-- (1Âª axioma) Para demostrar que univ âˆˆ F, sea e cualquier elemento de
-- L (existe por ser L no vacÃ­o). Por la definiciÃ³n de univ, se tiene
--    âˆ€ y, e â‰¤ y â†’ y âˆˆ univ
-- Por tanto, univ âˆˆ F.
--
-- (2Âº axioma) Sea X âˆˆ F e X âŠ† Y. De X âˆˆ F se deduce que existe un x âˆˆ L
-- tal que
--    âˆ€ y, x â‰¤ y â†’ y âˆˆ X
-- y, puesto que X âŠ† Y, se tiene que
--    âˆ€ y, x â‰¤ y â†’ y âˆˆ X
-- Por tanto, Y âˆˆ F.
--
-- (3Âº axioma) Sean X, Y âˆˆ F. Por la definiciÃ³n de F, existen x, y âˆˆ L
-- tales que
--    âˆ€ z, x â‰¤ z â†’ z âˆˆ X
--    âˆ€ z, y â‰¤ z â†’ z âˆˆ Y
-- Luego,
--    âˆ€ z, mÃ¡x(x,y) â‰¤ z â†’ z âˆˆ X âˆ© Y
-- Por tanto, X âˆ© Y âˆˆ F.

def at_top
  (L : Type)
  [linear_order L]
  (e : L)
  : filter L :=
{ sets := {X : set L | âˆƒ x : L, âˆ€ y, x â‰¤ y â†’ y âˆˆ X},
  univ_sets :=
  begin
    use e,
    intros y hy,
    exact mem_univ y,
  end,
  sets_of_superset :=
  begin
    rintros X Y âŸ¨x, hXâŸ© hXY,
    rw mem_set_of_eq,
    use x,
    intros y hxy,
    rw subset_def at hXY,
    apply hXY,
    exact hX _ hxy,
  end,
  inter_sets :=
  begin
    rintros X Y âŸ¨x, hXâŸ© âŸ¨y, hYâŸ©,
    use max x y,
    intros z hz,
    split,
    { apply hX,
      apply le_trans _ hz,
      exact le_max_left x y },
    { apply hY,
      apply le_trans _ hz,
      exact le_max_right x y, },
  end }

-- =====================================================================
-- Â§Â§ El filtro cofinito                                              --
-- =====================================================================

-- El *filtro cofinito* sobre un tipo `Î±` tiene como conjuntos los
-- subconjuntos `S : set Î±` con la propiedad de que `Sá¶œ`, el complemento
-- de `S`, es finito. Demostremos que es un filtro.
--
-- Lemas que pueden ser Ãºtiles en su demostraciÃ³n:
--    `compl_univ : univá¶œ = âˆ…`
--    `finite_empty : finite âˆ…`
--    `compl_subset_compl : Xá¶œ âŠ† Yá¶œ â†” Y âŠ† X`
--    `finite.subset : S.finite â†’ âˆ€ {T : set Î±}, T âŠ† S â†’ T.finite`
--    `compl_inter S T : (S âˆ© T)á¶œ = Sá¶œ âˆª Tá¶œ`
--    `finite.union : S.finite â†’ T.finite â†’ (S âˆª T).finite`
--
-- Nota. Si estÃ¡s pensando "Nunca podrÃ­a usar Lean por mÃ­ mismo, no
-- conozco los nombres de todos los lemas, asÃ­ que tengo que confiar en
-- que el enunciado me los diga". Se pueden encontrar los lemas
-- presionando MayÃºscula-espacio y las reglas para formar sus nombres.

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que el conjunto de los subconjuntos de Î± cuyo
-- complementario es finito es un filtro sobre Î±.
-- ---------------------------------------------------------------------

-- DemostraciÃ³n. Sea
--    F = {S : set Î± | Sá¶œ es finito}
-- Tenemos que demostrar que F es un filtro probando los tres axiomas de
-- filtro.
--
-- (1Âª axioma). Puesto que univá¶œ = âˆ…, que es finito, se tiene que
-- univ âˆˆ F.
--
-- (2Âº axioma). Sean S âˆˆ F y S âŠ† T. Entonces, Sá¶œ es finito y
-- Tá¶œ âŠ† Sá¶œ. Por tanto, Tá¶œ es finito y T âˆˆ F.
--
-- (3Âª axioma). Sean S, T âˆˆ F. Entonces, Sá¶œ y Tá¶œ son finitos y, puesto
-- que
--    (S âˆ© T)á¶œ = Sá¶œ âˆª Tá¶œ
-- tambiÃ©n (S âˆ© T)á¶œ es finito. Por tanto, S âˆ© T âˆˆ F.

def cofinite (Î± : Type) : filter Î± :=
{ sets := {S : set Î± | (Sá¶œ).finite},
  univ_sets :=
  begin
    rw mem_set_of_eq,
    rw compl_univ,
    exact finite_empty,
  end,
  sets_of_superset :=
  begin
    intros S T hS hST,
    rw mem_set_of_eq at *,
    rw â† compl_subset_compl at hST,
    exact finite.subset hS hST,
  end,
  inter_sets :=
  begin
    intros S T hS hT,
    rw mem_set_of_eq at *,
    rw compl_inter,
    exact finite.union hS hT,
  end }

-- =====================================================================
-- Â§ Ejercicios                                                       --
-- =====================================================================

-- No es necesario que sepas hacer los siguientes ejercicios para pasar
-- a la parte de topologÃ­a del tema. Los ejercicios son
-- 1. Demostrar que el filtro cofinito en un tipo finito es el filtro
--    del conjunto de potencias entero.
-- 2. Demostrar que el filtro cofinito sobre `â„•` es igual al filtro
--    de los subconjuntos grandes (i.e., `at_top`).
-- 3. Demostrar que el filtro cofinito sobre `â„¤` no es igual al filtro
--    de los subconjuntos grandes (i.e., `at_top`).
-- 4. Demostrar que el filtro cofinito sobre `â„•` no es principal.
--
-- Puedes probarlos en Lean pero tendrÃ¡s que dominar los conceptos de la
-- finitud. AquÃ­, por ejemplo, estÃ¡n algunas de las ideas que
-- necesitarÃ¡s para hacer (4) en Lean. La prueba utiliza varios lemas de
-- la API de conjuntos, como los siguientes:
-- + filter.ext_iff {Î± : Type u} {f g : filter Î±} :
--      f = g â†” âˆ€ (s : set Î±), s âˆˆ f â†” s âˆˆ g
-- + diff_eq_compl_inter {Î± : Type u} {s t : set Î±} :
--      s \ t = tá¶œ âˆ© s
-- + compl_inter {Î± : Type u} (s t : set Î±) :
--      (s âˆ© t)á¶œ = sá¶œ âˆª tá¶œ
-- + compl_compl {Î± : Type u} [boolean_algebra Î±] (x : Î±) :
--      xá¶œá¶œ = x
-- + set.finite_singleton {Î± : Type u} (a : Î±) :
--      {a}.finite
-- + mem_diff_singleton {Î± : Type u} {x y : Î±} {s : set Î±} :
--      x âˆˆ s \ {y} â†” x âˆˆ s âˆ§ x â‰  y
--
-- TambiÃ©n se necesitan los dos lemas siguientes que no estÃ¡n en mathlib:

lemma infinite_of_finite_compl
  {Î± : Type}
  [infinite Î±]
  {s : set Î±}
  (hs : sá¶œ.finite)
  : s.infinite :=
Î» h, set.infinite_univ (by simpa using hs.union h)

lemma set.infinite.nonempty
  {Î±}
  {s : set Î±}
  (h : s.infinite)
  : âˆƒ a : Î±, a âˆˆ s :=
let a := set.infinite.nat_embedding s h 37 in âŸ¨a.1, a.2âŸ©

lemma mem_cofinite
  {S : set â„•}
  : S âˆˆ cofinite â„• â†” Sá¶œ.finite :=
begin
  refl
end

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que el filtro cofinito sobre `â„•` no es
-- principal.
-- ---------------------------------------------------------------------

-- DemostraciÃ³n. Basta demostrar que para cualquier S âŠ† â„•, el filtro
-- principal de S no es el filtro cofinito. Lo haremos por reducciÃ³n al
-- absurdo. Para ello, supongamos que existe un S âŠ† â„• cuyo filtro
-- principal es el filtro cofinito; es decir,
--    {X : set â„• | S âŠ† X} = {X : set â„• | Xá¶œ es finito}               (1)
-- Luego, Sá¶œ es finito (ya que S âŠ† S) y, por tanto, S es infinito lo que
-- significa que existe un a âˆˆ S. Sea T = S \ {a}. Entonces,
--    Tá¶œ = (S \ {a})á¶œ = Sá¶œ âˆª {a}
-- lo que implica que Tá¶œ es finito y, por (1), S âŠ† T; es decir,
-- S âŠ† S \ {a} con a âˆˆ S lo que es una contradicciÃ³n.

theorem cofinite_not_principal :
  âˆ€ S : set â„•, cofinite â„• â‰  ğ“Ÿ S :=
begin
  intros S h,
  rw filter.ext_iff at h,
  have hS := h S,
  rw mem_cofinite at hS,
  have hS2 : Sá¶œ.finite,
  { rw hS,
    apply mem_principal_self },
  { clear hS,
    have hS3 := infinite_of_finite_compl hS2,
    have hS4 : âˆƒ s : â„•, s âˆˆ S := set.infinite.nonempty hS3,
    cases hS4 with a ha,
    set T := S \ {a} with hTdef,
    specialize h T,
    have hT : T âˆˆ cofinite â„•,
    { rw [mem_cofinite, hTdef, diff_eq_compl_inter, compl_inter, compl_compl],
      apply finite.union _ hS2,
      apply finite_singleton },
    rw h at hT,
    rw mem_principal_sets at hT,
    specialize hT ha,
    rw [hTdef, mem_diff_singleton] at hT,
    cases hT with _ hcontra,
    apply hcontra,
    refl, },
end
