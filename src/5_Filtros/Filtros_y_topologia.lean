-- ---------------------------------------------------------------------
-- Ejercicio. Importar la teorÃ­a de las propiedades de los subconjuntos
-- de espacios topolÃ³gicos.
-- ---------------------------------------------------------------------

import topology.subset_properties

-- ---------------------------------------------------------------------
-- Ejercicio. Declarar
-- + X e Y como variables sobre espacios topolÃ³gicos
-- + f como variable de funciones de X en Y.
-- ---------------------------------------------------------------------

variables (X Y : Type) [topological_space X] [topological_space Y]
variable  (f : X â†’ Y)

-- ---------------------------------------------------------------------
-- Ejercicio. Abrir las teorÃ­as filter y set.
-- ---------------------------------------------------------------------

open filter set

-- ---------------------------------------------------------------------
-- Ejercicio. Abrir localmente las teorÃ­as filter (para ğ“Ÿ) y
-- topological_space (para ğ“).
-- ---------------------------------------------------------------------

open_locale filter
open_locale topological_space

-- =====================================================================
-- Â§ Filtros de entornos                                              --
-- =====================================================================

-- Si `Î±` es un espacio topolÃ³gico y `a : Î±` entonces `ğ“ a` es el
-- siguiente filtro sobre `Î±`: `X âˆˆ ğ“ a` si y sÃ³lo si `X` contiene un
-- entorno abierto de `a` o, equivalentemente, si `a` estÃ¡ en el
-- interior de `X`. Hay que pensar en `ğ“ a` como el "subconjunto
-- generalizado" de `X` correspondiente a un entorno abierto
-- infinitesimal `a`.
--
-- Utilizaremos la API de interior y clausura, y comprobaremos que `ğ“ a`
-- es un filtro.
--
-- Los siguientes lemas de los espacios topolÃ³gicos serÃ¡n utiles:
-- + interior_univ {Î± : Type u} [topological_space Î±] :
--      interior univ = univ
-- + mem_univ {Î± : Type u} (x : Î±) :
--      x âˆˆ univ
-- + interior_mono {Î± : Type u} [topological_space Î±] {s t : set Î±} (h : s âŠ† t) :
--     interior s âŠ† interior t
-- + interior_inter {Î± : Type u} [topological_space Î±] {s t : set Î±} :
--     interior (s âˆ© t) = interior s âˆ© interior t

-- ---------------------------------------------------------------------
-- Ejercicio. Declarar Î± como una variable sobre espacios topolÃ³gicos.
-- ---------------------------------------------------------------------

variables {Î± : Type*} [topological_space Î±]

-- ---------------------------------------------------------------------
-- Ejercicio. Abrir el espacio de nombre de conjuntos.
-- ---------------------------------------------------------------------

open set

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que el conjunto de los entornos abiertos de un
-- punto a (en mathlib se denota por `ğ“ a`) es un filtro.
-- ---------------------------------------------------------------------

-- DemostraciÃ³n. Sea a un punto. El conjunto de los entornos abiertos de
-- a es
--    F = {X : set Î± | a âˆˆ interior X}.
-- Veamos que F cumple los axiomas de filtro.
--
-- (1Âº axioma) univ âˆˆ F, ya que a âˆˆ univ = interior(univ).
--
-- (2Âº axioma): Sean X âˆˆ F y X âŠ† Y. Entonces,
--    a âˆˆ interior(X) âŠ† interior(Y)
-- Luego, a âˆˆ interior(Y) y Y âˆˆ F.
--
-- (3Âº axioma): Sean X, Y âˆˆ F. Entonces,
--    a âˆˆ interior(X) y a âˆˆ interior(Y)
--    âŸ¹ a âˆˆ interior(X) âˆ© interior(Y) = interior(X âˆ© Y)
-- Por tanto, X âˆ© Y âˆˆ F.

example (a : Î±): filter Î± :=
{ sets := {X : set Î± | a âˆˆ interior X},
  univ_sets :=
  begin
    rw mem_set_of_eq,
    rw interior_univ,
    exact mem_univ a,
  end,
  sets_of_superset :=
  begin
    intros X Y haX hXY,
    rw mem_set_of_eq at *,
    exact interior_mono hXY haX,
  end,
  inter_sets :=
  begin
    intros X Y hX hY,
    rw mem_set_of_eq at *,
    rw interior_inter,
    exact âŸ¨hX, hYâŸ©
  end }

-- =====================================================================
-- Â§ Puntos de acumulaciÃ³n                                            --
-- =====================================================================

-- Un punto de acumulaciÃ³n `a : Î±` de un filtro `F : filter Î±` en un
-- espacio topolÃ³gico debe pensarse como un punto en la clausura del
-- "conjunto generalizado" correspondiente a `F`. Esta es la definiciÃ³n
-- formal.
--
-- Un punto de acumulaciÃ³n de un filtro `F : filter Î±` (tambiÃ©n conocido
-- como punto lÃ­mite) es `x : Î±` tal que `ğ“ x âŠ“ F â‰  âŠ¥`. La imagen es que
-- la intersecciÃ³n del conjunto generalizado `F` y el entorno abierto
-- `ğ“ x` de `x` tienen intersecciÃ³n no vacÃ­a. Sin embargo, repasemos la
-- notaciÃ³n con mÃ¡s cuidado. Recordemos que el orden de los filtros es
-- al revÃ©s, por lo que `ğ“ x âŠ“ F` significa el filtro generado por `F` y
-- los entornos de `x`, y `âŠ¥` es el filtro que contiene cada
-- subconjunto. AsÃ­ que esto se reduce a decir que no existen conjuntos
-- `A âˆˆ ğ“ x` y `B âˆˆ F` tales que `A âˆ© B = âˆ…`, o, en otras palabras, cada
-- elemento del filtro intersecta todos los entornos de `x`. Por
-- ejemplo, si `S` es un subconjunto cualquiera de `Î±`, entonces los
-- puntos de acumulaciÃ³n de `ğ“Ÿ S` son los puntos `x` tales que cualquier
-- conjunto abierto que contiene a `x` se encuentra con `S`, o lo que es
-- lo mismo, que `x` estÃ¡ en la cierre de `S`.
--
-- El siguiente lema (que se llama `cluster_pt.mono` en mathlib) afirma
-- que si `F` y `G` son subconjuntos generalizados de un espacio
-- topolÃ³gico y `F âŠ† G`, entonces `clausura(F) âŠ† clausura(G)`.

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que si x es un punto de acumulaciÃ³n del filtro F
-- y F â‰¤ G, entonces x es un punto de acumulaciÃ³n del filtro G.
-- ---------------------------------------------------------------------

-- 1Âª demostraciÃ³n. Sea `U âˆˆ ğ“ x` y `V âˆˆ G`. Entonces, `V âˆˆ F` (porque
-- `F â‰¤ G`) y `U âˆ© V â‰  âˆ…` (porque `x` es un punto de acumulaciÃ³n de
-- `F`). Por tanto, `x` es un punto de acumulaciÃ³n de `G`.

example
  {x : Î±}
  {F G : filter Î±}
  (hxF : cluster_pt x F)
  (hFG : F â‰¤ G)
  : cluster_pt x G :=
begin
  rw cluster_pt_iff at *,
  intros,
  apply hxF hU,
  rw filter.le_def at hFG,
  apply hFG _ hV,
end

-- 2Âª demostraciÃ³n. Se tiene
--    ğ“ x âŠ“ F â‰  âŠ¥          [porque x es un punto de acumulaciÃ³n de F]
--    ğ“ x âŠ“ F â‰¤ ğ“ x âŠ“ G    [porque F â‰¤ G]
--    ğ“ x âŠ“ G â‰  âŠ¥          [por los dos anteriores]
-- Por tanto, x es un punto de acumulaciÃ³n de G.

example
  {x : Î±}
  {F G : filter Î±}
  (hxF : cluster_pt x F)
  (hFG : F â‰¤ G)
  : cluster_pt x G :=
begin
  unfold cluster_pt,
  apply ne_bot_of_le_ne_bot hxF,
  exact inf_le_inf_left _ hFG,
end

-- 3Âª demostraciÃ³n
example
  {x : Î±}
  {F G : filter Î±}
  (hxF : cluster_pt x F)
  (hFG : F â‰¤ G)
  : cluster_pt x G :=
-- by library_search
cluster_pt.mono hxF hFG

-- =====================================================================
-- Â§ Compacidad                                                       --
-- =====================================================================

-- La definiciÃ³n actual de `is_compact` en mathlib dice que un
-- subconjunto `S` de un espacio topolÃ³gico espacio `Î±` es *compacto` si
-- para cada filtro `F â‰  âŠ¥` tal que `S âˆˆ F` existe `a : Î±` tal que cada
-- conjunto de `F` comparte elementos con cada entorno de `a`. En otras
-- palabras, `S` es compacto si el cierre de cada subconjunto
-- generalizado no vacÃ­o `F âŠ† S` contiene algÃºn elemento de `S`. De
-- alguna manera, la no compacidad da lugar a subconjuntos generalizados
-- que estÃ¡n "en el lÃ­mite de `S`" pero que no intersectan un entorno de
-- elementos de "S". Tal vez sea mejor no preocuparse por esta exÃ³tica
-- definiciÃ³n. Por supuesto es equivalente a la definiciÃ³n habitual de
-- compacidad, pero no vamos a demostrarlo.
--
-- Vamos a volver a probar que un subconjunto cerrado de un espacio
-- compacto es compacto. Como antes, demostramos la afirmaciÃ³n mÃ¡s
-- general de que si `Î±` es cualquier espacio topolÃ³gico, la
-- intersecciÃ³n de un subconjunto compacto de `Î±` y un subconjunto
-- cerrado de `Î±` es un subconjunto compacto de `Î±`.
--
-- Esta es la definiciÃ³n en mathlib:
--    def is_compact (s : set Î±) := âˆ€ â¦ƒfâ¦„ [ne_bot f], f â‰¤ ğ“Ÿ s â†’ âˆƒaâˆˆs, cluster_pt a f
--
-- NÃ³tese que `ne_bot f` estÃ¡ entre corchetes, lo que significa que el
-- sistema de inferencia de tipos lo deduce.
--
-- Una sugerencia para una prueba es primero mostrar que por la
-- compacidad de `S`, podemos encontrar un punto de acumulaciÃ³n `a` para
-- `f` en `s` y despuÃ©s demostrar que este punto de acumulaciÃ³n estÃ¡ en
-- `t` tambiÃ©n, porque "t" es cerrado.

-- Algunos lemas Ãºtiles son:
-- + is_closed.closure_eq
--     {Î± : Type u} [topological_space Î±]
--     {s : set Î±}
--     (h : is_closed s)
--     : closure s = s
-- + mem_closure_iff_cluster_pt
--     {Î± : Type u} [topological_space Î±]
--     {s : set Î±}
--     {a : Î±}
--     : a âˆˆ closure s â†” cluster_pt a (ğ“Ÿ s)

-- ---------------------------------------------------------------------
-- Ejercicio. Demostrar que la intersecciÃ³n de un compacto s con un
-- cerrado (t) es un compacto (s âˆ© t).
-- ---------------------------------------------------------------------

-- DemostraciÃ³n. Sea `f` un filtro tal que `f â‰  âŠ¥` y `f â‰¤ ğ“Ÿ (s âˆ© t)`.
-- Tenemos que demostrar que existe `a âˆˆ s âˆ© t` que es un punto de
-- acumulaciÃ³n de `f`.
--
-- De `f â‰¤ ğ“Ÿ (s âˆ© t)` se sigue que `f â‰¤ ğ“Ÿ s` y, por ser `s`  compacto,
-- existe `a âˆˆ s` que es un punto de acumulaciÃ³n de `f`. Pra concluir la
-- demostraciÃ³n, basta demostrar que `a âˆˆ t` (ya que entonces
-- `a âˆˆ s âˆ© t`). Su demostraciÃ³n es
--    s âˆ© t âŠ† s
--    âŸ¹ ğ“Ÿ (s âˆ© t) â‰¤ ğ“Ÿ t
--           [por principal_mono]
--    âŸ¹ f â‰¤ ğ“Ÿ t
--           [porque f â‰¤ ğ“Ÿ (s âˆ© t)]
--    âŸ¹ a es un punto de acumulaciÃ³n de ğ“Ÿ t
--           [por cluster_pt.mono y serlo de f]
--    âŸ¹ a âˆˆ clausura(t)
--           [por mem_closure_iff_cluster_pt]
--    âŸ¹ a âˆˆ t
--           [por closure_eq_iff_is_closed.mpr y ser t cerrado]
--

lemma closed_of_compact
  (s : set X)
  (hs : is_compact s)
  (t : set X)
  (ht : is_closed t)
  : is_compact (s âˆ© t) :=
begin
  unfold is_compact,
  intros f hnf hstf,
  haveI := hnf,
  obtain âŸ¨a, has, haâŸ© : âˆƒ a âˆˆ s, cluster_pt a f,
  { unfold is_compact at hs,
    apply hs,
    apply le_trans hstf,
    rw principal_mono,
    apply inter_subset_left },
  have hat : a âˆˆ t,
  { rw â† closure_eq_iff_is_closed.mpr ht,
    rw mem_closure_iff_cluster_pt,
    apply cluster_pt.mono ha,
    refine le_trans hstf _,
    rw principal_mono,
    apply inter_subset_right },
  exact âŸ¨a, âŸ¨has, hatâŸ©, haâŸ©
end
